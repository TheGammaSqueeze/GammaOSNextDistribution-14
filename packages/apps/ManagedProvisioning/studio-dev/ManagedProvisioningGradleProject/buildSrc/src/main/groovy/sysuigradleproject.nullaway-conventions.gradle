import net.ltgt.gradle.errorprone.CheckSeverity

plugins {
    id 'net.ltgt.errorprone'
    id 'net.ltgt.nullaway'
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ["-Xmaxerrs", "10000"] // Display all errors
    if (name.contains("Nullsafe")) {
        options.errorprone.disableAllChecks = true
        options.errorprone.nullaway {
            severity = CheckSeverity.ERROR

            // Control the packages and classes that are treated as annotated
            annotatedPackages.add("com.android.systemui.qs")

            // Ignore Dagger-generated classes
            excludedClassAnnotations.add("dagger.internal.DaggerGenerated")
        }
    } else {
        options.errorprone.enabled = false
    }
}

dependencies {
    annotationProcessor "com.uber.nullaway:nullaway:0.9.1"
    annotationProcessor "com.google.errorprone:error_prone_core:2.4.0"
}
